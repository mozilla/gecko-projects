# Copyright (c) 2015 The WebRTC project authors. All Rights Reserved.
#
# Use of this source code is governed by a BSD-style license
# that can be found in the LICENSE file in the root of the source
# tree. An additional intellectual property rights grant can be found
# in the file PATENTS.  All contributing project authors may
# be found in the AUTHORS file in the root of the source tree.

import("//testing/test.gni")

template("webrtc_fuzzer_test") {
  assert(defined(invoker.sources), "Need sources in $target_name.")

  test(target_name) {
    forward_variables_from(invoker, [ "sources" ])
    deps = [
      ":webrtc_fuzzer_main",
    ]
    if (defined(invoker.deps)) {
      deps += invoker.deps
    }
    if (is_clang) {
      # Suppress warnings from Chrome's Clang plugins.
      # See http://code.google.com/p/webrtc/issues/detail?id=163 for details.
      configs -= [ "//build/config/clang:find_bad_constructs" ]
    }
  }
}
